# Access via MACRO_IMPORT bytecodemanipulation.standard_library
# and than either CALL MACRO std:..., or for macros marked with ASSEMBLY, directly with std:...


NAMESPACE std
{

    # Wrapper around the print builtin
    MACRO ASSEMBLY print(text VARIABLE_ARG)
    {
        CALL ~print (*&text)
        POP
    }

    # Wrapper around the input builtin
    MACRO ASSEMBLY input(result VARIABLE, text VARIABLE_ARG)
    {
        CALL ~input (*&text) -> &result
    }

    MACRO ASSEMBLY check_type(expected_type, value, error_message)
    {
        IF OP (not ~isinstance(&value, &expected_type))
        {
            RAISE ~ValueError(&error_message)
        }
    }

    MACRO ASSEMBLY check_type(expected_type, value)
    {
        check_type(&expected_type, &value, OP ("Invalid type, expected " + ~repr(&value)))
    }

    NAMESPACE os
    {
        MACRO ASSEMBLY file_walker(directory, file VARIABLE, code CODE_BLOCK)
        {
            CALL ~os.listdir(&directory) -> $MACRO_files

            LOAD 0 -> $MACRO_counter
            CALL ~len($MACRO_files) -> $MACRO_file_count

            WHILE OP ($MACRO_counter < $MACRO_file_count)
            {
                LOAD $MACRO_files[$MACRO_counter] -> &file
                OP $MACRO_counter + 1 -> $MACRO_counter

                IF ~os.path.isfile(&file)
                {
                    MACRO_PASTE &code
                }
            }
        }
    }

    NAMESPACE comprehension
    {
        MACRO ASSEMBLY list(iterable, var VARIABLE, code CODE_BLOCK) -> ANY
        {
            DEF MACRO_inner<&iterable>(iter_obj)
            {
                LOAD ~list() -> $__unique
                FOREACH &var IN &iterable
                {
                    MACRO_PASTE code
                    CALL $__unique.append(%) -> \
                }
                RETURN $__unique
            }

            MACRO_RETURN $MACRO_inner(~iter(&iterable))
        }
    }

    NAMESPACE stream
    {
        MACRO ASSEMBLY initialize(var VARIABLE)
        {
            LOAD ~list() -> &var
        }

        MACRO ASSEMBLY extend(var VARIABLE, value)
        {
            CALL &var.extend(&value) -> \
        }

        MACRO ASSEMBLY filter(var VARIABLE, predicate_source VARIABLE, predicate CODE_BLOCK)
        {
            DEF MACRO_filtered(item)
            {
                LOAD $item -> &predicate_source
                MACRO_PASTE predicate
                RETURN %
            }

            LOAD &var.copy() -> $MACRO_tmp
            CALL &var.clear() -> \
            CALL &var.extend(~filter($MACRO_filtered, $MACRO_tmp)) -> \
        }

        MACRO ASSEMBLY map(var VARIABLE, predicate_source VARIABLE, predicate CODE_BLOCK)
        {
            DEF MACRO_mapping(item)
            {
                LOAD $item -> &predicate_source
                MACRO_PASTE predicate
                RETURN %
            }

            LOAD &var.copy() -> $MACRO_tmp
            CALL &var.clear() -> \
            CALL &var.extend(~map($MACRO_mapping, $MACRO_tmp)) -> \
        }

        MACRO ASSEMBLY reduce(var VARIABLE, lhs VARIABLE, rhs VARIABLE, reducer CODE_BLOCK)
        {
            IF OP (~len(&var) == 0)
            {
                RETURN -1
            }

            LOAD &var[0] -> $MACRO_tmp

            LOAD 1 -> $MACRO_index
            LOAD ~len(&var) -> $MACRO_max_size

            WHILE OP ($MACRO_index < $MACRO_max_size)
            {
                LOAD $MACRO_tmp -> &lhs
                LOAD &var[$MACRO_index] -> &rhs
                MACRO_PASTE &reducer
                STORE $MACRO_tmp
                OP $MACRO_index + 1 -> $MACRO_index
            }

            LOAD $MACRO_tmp
        }

        MACRO ASSEMBLY to_list(var VARIABLE, output VARIABLE)
        {
            LOAD ~list(&var) -> &output
        }
    }
}
